# Please report bugs regarding the PKGBUILD at
# https://github.com/ystein/archlinux-aur

# Maintainer: Yannik Stein <yannik.stein [at] gmail.com>
# Submitter: vbPadre <vbpadre [at] gmail.com>

pkgname=ajenti-git
pkgver=20121002
pkgrel=1
pkgdesc="An easy server administration frontend like webmin. Please report \
bugs regarding the PKGBUILD at https://github.com/ystein/archlinux-aur."

arch=('any')
url="http://github.com/Eugeny/ajenti/"
license=('GPL')
depends=( 'python2' 'python2-lxml' 'python2-pyopenssl'
          'python2-feedparser' 'python2-gevent' 'python2-passlib' )
makedepends=('git' 'python2' 'python2-distribute')
optdepends=('python2-psutil: Task Manager plugin'
            'python2-beautifulsoup3: Munin plugin')
provides=('ajenti')
install='ajenti.install'

_gitroot=git://github.com/Eugeny/ajenti.git
_gitname=ajenti
_gitbranch=dev

build() {
  msg2 "Connecting to GIT server...."
  if [[ ! -d "${_gitname}" ]] ; then
    git clone "${_gitroot}" "${_gitname}"
  fi
  cd "${_gitname}"
  git reset --hard
  git checkout "${_gitbranch}"
  git pull
  msg2 "The local files are updated."
  msg2 "GIT checkout done or server timeout."

  find . -name '*.py' -exec sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "{}" \;
}

package() {
  cd "${_gitname}"
  python2 setup.py install --root "$pkgdir"
  rm -r "$pkgdir/etc/init.d"
  install -D "packaging/files/ajenti.arch" "$pkgdir/etc/rc.d/ajenti"
  chmod o-rwx "$pkgdir/etc/ajenti/ajenti.conf"
}
